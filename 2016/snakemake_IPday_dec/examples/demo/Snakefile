# Data files can be found here for example:
#  - http://www.nefsc.noaa.gov/psb/acoustics/sounds.html
#  - http://www.dosits.org/animals/useofsound/mammalscommunicate/individualspecific/
configfile: "config.yaml"

samples = config['samples']
window  = config['windows']

rule all:
    input:
        expand("{dataset}_{window}.png", dataset=samples, window=window),
        "server.ready"

rule server:
    output: touch("server.ready")
    params:
        port=config['port']
    run:
        from easydev import browser
        browser.browse("http://127.0.0.1:%s" % params['port'])

rule spectrogram:
    input: "{dataset}.wav"
    output: "{dataset}_{window}.png"
    log: "logs/{dataset}_{window}.log"
    shell: "python spectrogram.py {input} {wildcards.window} > {log}"

rule copy:
    output: "{dataset}.wav"
    shell: "cp ../data/{wildcards.dataset}.wav ."

rule clean:
    shell: "rm -f *png *.wav"

onsuccess:
    import glob
    if len(glob.glob("*.png")):
        from myapp import SpecExample
        app = SpecExample(samples, window, ".")
        app.launch(port=config['port'])
